plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.11'
    id 'org.beryx.jlink' version '2.25.0'
    id "com.google.osdetector" version "1.7.0"
}

mainClassName = "org.scenicview.ScenicView"
group = 'org.scenic-view'
version = '11.0.2'

defaultTasks 'install'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

repositories {
    mavenCentral()
}

configurations {
    jdk
    binTray
}

sourceSets {
    main {
        compileClasspath += configurations.jdk
    }
}

javafx {
    version = "17.0.2"
    modules = ['javafx.web', 'javafx.fxml', 'javafx.swing']
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.scenicview.ScenicView',            /* Used for standalone Scenic View */
                'Agent-Class': 'org.fxconnector.remote.RuntimeAttach',/* But the standalone Scenic View starts up this agent when in runtime attach mode */
                'Premain-Class': 'org.scenicview.ScenicView',          /* Alternatively, this will be called if Scenic View is started as an agent */
                'Automatic-Module-Name': 'org.scenicview.scenicview'
        )
    }
    archiveName = 'scenicview.jar'
}

run {
	dependsOn jar
}

artifacts {
    archives(jar)
}

ext.platform = osdetector.os == 'osx' ? "mac-${osdetector.arch}" : osdetector.os == 'windows' ? 'win' : osdetector.os

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'scenicView'
    }
    imageDir = file("${buildDir}/scenicview")
    imageZip = file("${buildDir}/dist/scenicview-${platform}.zip")
}

tasks.jlink.doLast {
    // Copy scenicView.jar to lib, it is required as loading Agent
    copy {
        from "${buildDir}/libs/scenicview.jar"
        into "${buildDir}/scenicview/lib"
    }
}